// 意念之石是一件强大的圣遗物,以至于经常会在妄图使用它的人们手中失控.
// 启动后,它持续性地执行给予它的指令.
// 指令必须使用石之脚本来编写.
// 你可以在 StoneStoryRPG.com/stonescript 找到相关指南.
// ——贤者贝赛拉

import Cosmetics/PetBoo
//import Cosmetics/Hats/skully
import fishing

? !loc.isQuest
    ? !(foe.state = -1)
        draw.Box(1, 6, 15, 5, #888888, 1)
        >`2,7,name: @foe.name@
        >`2,8,dist: @foe.distance@
        >`2,9,state:@foe.state@

    draw.Box(1, 18, 15, 6, #888888, 1)
    >`2,19,buff:   @buffs.string@
    >`2,20,debuff: @debuffs.string@
    >`2,21,left:   @item.left@
    >`2,22,right:  @item.right@

? loc = Temple
    brew wood + tar
    ? foe.distance > 23 | foe.distance = -1
        equipL 星之石
        equipR 衔尾蛇之石
    :? foe.name = 吞炬蛇神那伽王
        equip 连弩
        ? debuffs.count >= 6
            activate potion
    :? debuffs.count > 1
        equipL 寒冰魔杖
        equipR 寒冰盾牌
    :
        equip 连弩

? loc = Boiling Mine
    var jump_back_time = -1
    brew wood + bronze
    ? foe.name = 青铜守卫
        ? foe.state = 1
            equipL 星之石
            equipR 衔尾蛇之石
        :? foe.state = 32 & jump_back_time < 0
            jump_back_time = time + 30
        :? foe.state = 32 & jump_back_time > 0 & time >= jump_back_time
            equip 意念之石
        :? foe.state = 33
            jump_back_time = -1
            ? foe.armor > 0
                equip 重锤
                ? foe.distance = 8 & item.GetCooldown("hammer") <= 0
                    activate R
                ? foe.distance = 8 & item.GetCooldown("hammer") > 0
                    activate potion
            :
                equip 连弩
                // activate potion
        :
            equip 连弩
    :? foe.distance > 23 | foe.distance = -1
        equipL 星之石
        equipR 衔尾蛇之石
    :
        equip 连弩

? loc = Mushroom Forest
    brew tar
    ? hp < 10
        activate potion
    ? foe.distance > 23 | foe.distance = -1
        equipL 星之石
        equipR 衔尾蛇之石
    :? foe.armor > 0 & foe.distance <= 8
        equip 重锤
        ? item.GetCooldown("hammer") <= 0
            activate R
    :
        equip 连弩

? loc = Caves of Fear
    ? foe.distance > 23 | foe.distance = -1
        equipL 星之石
        equipR 衔尾蛇之石
    :? foe.name = 悬蝠
        equipL 弩
        equipR 寒冰盾牌
    :
        equip 连弩
